## -----------------------------------------------------------------------------
##  Simulate Human data
## -----------------------------------------------------------------------------
## 
## Software requirements:
## RSEM
## bowtie2
## R
## R packages: gtools, dplyr, eisaR, Biostrings, GenomicFeatures, BSgenome
## 
## To download the required data, first run 
## snakemake --cores 1 get_data
## 
## Then, to run the simulations:
## snakemake --cores 16 --use-envmodules -npr
## If software is managed via a module system, edit module names below
## If software is not managed via modules, remove --use-envmodules from the command
## 
## -----------------------------------------------------------------------------

## Modules 
Rmod = "R-BioC/4.2-3.15-foss-2020b"
Pandocmod = "Pandoc/2.10"
RSEMmod = "RSEM/1.3.3-foss-2019b"
bowtie2mod = "Bowtie2/2.3.5.1-GCC-8.3.0"
Salmonmod = "Salmon/1.6.0"
kallistomod = "kallisto/0.46.0-foss-2019b"
STARmod = "STAR/2.7.9a-GCC-10.3.0"
Rbin = "R CMD BATCH --no-restore --no-save"

## -----------------------------------------------------------------------------
## Enumerate simulations to do
## Format: de{nde}_ds{nds}_dr{ndr}_drbatch{ndrbatch}_off{offset}, 
## where {nde}, {nds}, {ndr} are the number of 
## genes affected by differential expression/splicing/regulation, respectively,
## {ndrbatch} is the number of genes affected by batch, and {offset} determines
## the fold change for the DE genes.
## -----------------------------------------------------------------------------
sims = ["de0_ds0_dr0_drbatch0_off2", "de0_ds0_dr2000_drbatch0_off2", "de2000_ds0_dr2000_drbatch0_off2", "de0_ds2000_dr2000_drbatch0_off2"]
simsadd = ["de2000_ds0_dr2000_drbatch0_off5", "de2000_ds0_dr2000_drbatch0_off8"]
simsbatch = ["de0_ds0_dr2000_drbatch2000_off2"]

## -----------------------------------------------------------------------------
##  Default rule
## -----------------------------------------------------------------------------
rule all:
    input:
        "Rout/prepare_expanded_reference_files.Rout",
        "reference/rsem_reference/gencode.v41.annotation.protein_coding.expanded.rev.2.bt2l",
        "reference/rsem_model/SRR493366.stat/SRR493366.model",
        "reference/rsem_model/SRR493366.stat/SRR493366.highQ.model",
        expand("Rout/generate_rsem_files_{simtype}.Rout", simtype = sims),
        expand("{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz", simtype = sims, i = ["1","2","3","4","5","6"]),
        "reference/gencode.v41.annotation.protein_coding.expanded.k31.sidx/info.json",
        expand("{simtype}/2_quants/salmon/sample{i}/quant.sf", simtype = sims, i = ["1","2","3","4","5","6"]),
        "reference/GRCh38.primary_assembly_gencode.v41.annotation.protein_coding.expanded.starindex/SA",
        expand("{simtype}/2_quants/star/sample{i}/Aligned.toTranscriptome.out.bam", simtype = sims, i = ["1","2","3","4","5","6"]),
        expand("{simtype}/2_quants/salmon_star/sample{i}/quant.sf", simtype = sims, i = ["1","2","3","4","5","6"]),
        expand("{simtype}/2_quants/kallisto_quant/sample{i}/abundance.tsv", simtype = sims, i = ["1","2","3","4","5","6"]),
        expand("{simtype}/2_quants/kallisto_pseudo/sample{i}/pseudoalignments.tsv", simtype = sims, i = ["1","2","3","4","5","6"]),
        ## Generate small example data set
        "reference/gencode.v41.annotation.protein_coding.expanded.transcripts_chr22_1_35000000.fa",
        "reference/gencode.v41.annotation.protein_coding.expanded_chr22_1_35000000.k31.sidx/info.json",
        expand("{simtype}/2_quants/salmon_chr22_1_35000000/sample{i}/quant.sf", simtype = ["de0_ds0_dr2000_drbatch0_off2"], i = ["1","2","3","4","5","6"]),

## Additional simulations with higher DE logFCs
rule addsim:
    input:
        expand("Rout/generate_rsem_files_{simtype}.Rout", simtype = simsadd),
        expand("{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz", simtype = simsadd, i = ["1","2","3","4","5","6"]),
        expand("{simtype}/2_quants/salmon/sample{i}/quant.sf", simtype = simsadd, i = ["1","2","3","4","5","6"]),

## Additional simulations with batch effect
rule batchsim:
    input:
        expand("Rout/generate_rsem_files_{simtype}.Rout", simtype = simsbatch),
        expand("{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz", simtype = simsbatch, i = ["1","2","3","4","5","6"]),
        expand("{simtype}/2_quants/salmon/sample{i}/quant.sf", simtype = simsbatch, i = ["1","2","3","4","5","6"]),

## -----------------------------------------------------------------------------
##  Download data
## -----------------------------------------------------------------------------
rule get_data:
    shell:
        '''
        wget -P fastq ftp://ftp.sra.ebi.ac.uk/vol1/fastq/SRR493/SRR493366/SRR493366_1.fastq.gz
        wget -P fastq ftp://ftp.sra.ebi.ac.uk/vol1/fastq/SRR493/SRR493366/SRR493366_2.fastq.gz
        wget -P reference https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_41/GRCh38.primary_assembly.genome.fa.gz
        wget -P reference https://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_41/gencode.v41.annotation.gtf.gz
        gunzip reference/gencode.v41.annotation.gtf.gz
        gunzip reference/GRCh38.primary_assembly.genome.fa.gz
        grep "protein_coding" reference/gencode.v41.annotation.gtf > reference/gencode.v41.annotation.protein_coding.gtf
        '''

## -----------------------------------------------------------------------------
##  Prepare "expanded" reference files
## -----------------------------------------------------------------------------
rule prepare_reference_files:
    input:
        genome = "reference/GRCh38.primary_assembly.genome.fa",
        gtf = "reference/gencode.v41.annotation.protein_coding.gtf",
        script = "../scripts/prepare_expanded_reference_files.R"
    output:
        "reference/gencode.v41.annotation.protein_coding.expanded.tx2gene.tsv",
        "reference/gencode.v41.annotation.protein_coding.expanded.transcripts.fa",
        "reference/gencode.v41.annotation.protein_coding.expanded.gtf",
        rout = "Rout/prepare_expanded_reference_files.Rout"
    params:
        outdir = lambda wildcards, output: os.path.dirname(output[0])
    envmodules:
        f"{Rmod}"
    shell:
        '''
        {Rbin} "--args genome='{input.genome}' gtf='{input.gtf}' outdir='{params.outdir}'" {input.script} {output.rout}
        '''

## -----------------------------------------------------------------------------
##  Build RSEM index
## -----------------------------------------------------------------------------
rule prepare_rsem_reference:
    input:
        genome = "reference/GRCh38.primary_assembly.genome.fa",
        gtf = "reference/gencode.v41.annotation.protein_coding.expanded.gtf"
    output:
        "reference/rsem_reference/gencode.v41.annotation.protein_coding.expanded.rev.2.bt2l"
    params:
        refname = lambda wildcards, output: output[0].replace(".rev.2.bt2l", "")
    envmodules:
        f"{RSEMmod}",
        f"{bowtie2mod}"
    shell:
        '''
        rsem-prepare-reference --gtf {input.gtf} --bowtie2 {input.genome} {params.refname}
        '''

## -----------------------------------------------------------------------------
##  Estimate RSEM model file
## -----------------------------------------------------------------------------
rule estimate_rsem_modelfile:
    input:
        r1 = "fastq/SRR493366_1.fastq.gz",
        r2 = "fastq/SRR493366_2.fastq.gz",
        index = "reference/rsem_reference/gencode.v41.annotation.protein_coding.expanded.rev.2.bt2l"
    output:
        log = "reference/rsem_model/SRR493366.log",
        modelFile = "reference/rsem_model/SRR493366.stat/SRR493366.model",
        isoformResultsFile = "reference/rsem_model/SRR493366.isoforms.results"
    params:
        index = lambda wildcards, input: input["index"].replace(".rev.2.bt2l", ""),
        outprefix = lambda wildcards, output: output["log"].replace(".log", "")
    threads:
        32
    envmodules:
        f"{RSEMmod}",
        f"{bowtie2mod}"
    shell:
        '''
        rsem-calculate-expression --paired-end --bowtie2 -p {threads} --seed 123 {input.r1} {input.r2} \
        {params.index} {params.outprefix}
        '''

## 'Fix' model file as was done in Soneson & Matthes et al (2016), to avoid long stretches of low 
## quality bases in the simulated data
rule fix_rsem_modelfile:
    input:
        modelFile = "reference/rsem_model/SRR493366.stat/SRR493366.model",
        script = "../scripts/fix_rsem_model_file.R"
    output:
        modelFile = "reference/rsem_model/SRR493366.stat/SRR493366.highQ.model",
        rout = "Rout/fix_rsem_modelfile.Rout"
    envmodules:
        f"{Rmod}"
    shell:
        '''
        {Rbin} "--args inputFile='{input.modelFile}' outputFile='{output.modelFile}'" {input.script} {output.rout}
        '''

## -----------------------------------------------------------------------------
##  Generate simulation files
## -----------------------------------------------------------------------------
rule generate_simulation_parameters:
    input:
        isoformResultsFile = "reference/rsem_model/SRR493366.isoforms.results",
        script = "../scripts/generate_rsem_files.R"
    output:
        expand("de{{nde}}_ds{{nds}}_dr{{ndr}}_drbatch{{ndrbatch}}_off{{offset}}/1_reads/rsem_files/sample{i}.txt", i=["1","2","3","4","5","6"]),
        "de{nde}_ds{nds}_dr{ndr}_drbatch{ndrbatch}_off{offset}/3_truth/simulation_details.txt",
        rout = "Rout/generate_rsem_files_de{nde}_ds{nds}_dr{ndr}_drbatch{ndrbatch}_off{offset}.Rout"
    params:
        seed = 123,
        nbrPerGroup = 3,
        meanDispFile = "reference/Pickrell.Cheung.Mu.Phi.Estimates.rds",
        outdirBase = lambda wildcards: "de" + wildcards.nde + "_ds" + wildcards.nds + "_dr" + wildcards.ndr + "_drbatch" + wildcards.ndrbatch + "_off" + wildcards.offset,
        librarySize = 40000000,
        nbrDiffReg = lambda wildcards: wildcards.ndr,
        nbrDiffSpliced = lambda wildcards: wildcards.nds,
        nbrDiffExpr = lambda wildcards: wildcards.nde,
        nbrDRBatch = lambda wildcards: wildcards.ndrbatch,
        fcOffset = lambda wildcards: wildcards.offset
    envmodules:
        f"{Rmod}"
    shell:
        '''
        {Rbin} "--args seed={params.seed} isoform_results_file='{input.isoformResultsFile}' nbr_per_group={params.nbrPerGroup} mean_disp_file='{params.meanDispFile}' outdir_base='{params.outdirBase}' library_size={params.librarySize} nbr_diff_reg={params.nbrDiffReg} nbr_diff_spliced={params.nbrDiffSpliced} nbr_diff_expr={params.nbrDiffExpr} diff_expr_fold_change_offset={params.fcOffset} nbr_dr_batch={params.nbrDRBatch}" {input.script} {output.rout}
        '''

## -----------------------------------------------------------------------------
##  Simulate fastq files
## -----------------------------------------------------------------------------
rule simulate_reads:
    input:
        isoformResultsFile = "{simtype}/1_reads/rsem_files/sample{i}.txt",
        index = "reference/rsem_reference/gencode.v41.annotation.protein_coding.expanded.rev.2.bt2l",
        modelFile = "reference/rsem_model/SRR493366.stat/SRR493366.highQ.model",
    output:
        "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz"
    params:
        index = lambda wildcards, input: input["index"].replace(".rev.2.bt2l", ""),
        librarySize = 40000000,
        outdirBase = lambda wildcards, output: output[0].replace("_1.fq.gz", ""),
        theta0 = 0.05,
        seed = 123
    envmodules:
        f"{RSEMmod}",
        f"{bowtie2mod}"
    shell:
        '''
        rsem-simulate-reads {params.index} {input.modelFile} {input.isoformResultsFile} \
        {params.theta0} {params.librarySize} {params.outdirBase} --seed {params.seed} && \
        gzip {params.outdirBase}_1.fq && \
        gzip {params.outdirBase}_2.fq
        '''

## -----------------------------------------------------------------------------
## Build Salmon index
## -----------------------------------------------------------------------------
rule get_chrnames:
    input:
        genome = "reference/GRCh38.primary_assembly.genome.fa"
    output: 
        "reference/GRCh38.primary_assembly.chromosome_names.txt"
    shell:
        '''
        grep ">" {input.genome} | cut -d ">" -f 2 | cut -d " " -f 1 > {output}
        '''

rule build_salmon_index:
    input:
        txome = "reference/gencode.v41.annotation.protein_coding.expanded.transcripts{chr}.fa",
        genome = "reference/GRCh38.primary_assembly.genome{chr}.fa",
        chrnames = "reference/GRCh38.primary_assembly.chromosome_names{chr}.txt"
    output:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded{chr,.*}.k31.sidx/info.json"
    params:
        idxdir = lambda wildcards, output: os.path.dirname(output["idx"])
    threads:
        16
    envmodules:
        f"{Salmonmod}"
    shell:
        '''
        salmon index -t <(cat {input.txome} {input.genome}) -k 31 \
        -i {params.idxdir} --gencode -p {threads} -d {input.chrnames}
		'''

## Subset annotation to only chr22 to create a small example data set
## Potentially, could also subset the fastq files (we know from where all reads come)
rule subset_genome_txome:
    input:
        txome = "reference/gencode.v41.annotation.protein_coding.expanded.transcripts.fa",
        genome = "reference/GRCh38.primary_assembly.genome.fa",
        gtf = "reference/gencode.v41.annotation.protein_coding.expanded.gtf",
        script = "../scripts/subset_genome_txome.R"
    output:
        txome = "reference/gencode.v41.annotation.protein_coding.expanded.transcripts_{chr}.fa",
        genome = "reference/GRCh38.primary_assembly.genome_{chr}.fa",
        chrnames = "reference/GRCh38.primary_assembly.chromosome_names_{chr}.txt"
    params:
        coordkeep = "{chr}",
        rout = "Rout/subset_genome_txome_{chr}.Rout"
    threads:
        1
    envmodules:
        f"{Rmod}"
    shell:
        '''
        {Rbin} "--args genomein='{input.genome}' genomeout='{output.genome}' txomein='{input.txome}' txomeout='{output.txome}' chrnameout='{output.chrnames}' gtfin='{input.gtf}' coordkeep='{params.coordkeep}'" {input.script} {params.rout}
        '''

## -----------------------------------------------------------------------------
## Quantify with Salmon
## -----------------------------------------------------------------------------
rule run_salmon:
    input:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded.k31.sidx/info.json",
        r1 = "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        r2 = "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz"
    output:
        quant = "{simtype}/2_quants/salmon/sample{i}/quant.sf"
    params:
        idxdir = lambda wildcards, input: os.path.dirname(input["idx"]),
        outdir = lambda wildcards, output: os.path.dirname(output["quant"])
    threads:
        16
    envmodules:
        f"{Salmonmod}"
    shell:
        '''
        salmon quant --index {params.idxdir} --libType A -1 {input.r1} -2 {input.r2} \
        --threads {threads} --output {params.outdir} --dumpEq --numGibbsSamples 30
        '''

rule run_salmon_subset:
    input:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded_chr22_1_35000000.k31.sidx/info.json",
        r1 = "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        r2 = "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz"
    output:
        quant = "{simtype}/2_quants/salmon_chr22_1_35000000/sample{i}/quant.sf"
    params:
        idxdir = lambda wildcards, input: os.path.dirname(input["idx"]),
        outdir = lambda wildcards, output: os.path.dirname(output["quant"])
    threads:
        16
    envmodules:
        f"{Salmonmod}"
    shell:
        '''
        salmon quant --index {params.idxdir} --libType A -1 {input.r1} -2 {input.r2} \
        --threads {threads} --output {params.outdir} --dumpEq --numGibbsSamples 30
        '''

## -----------------------------------------------------------------------------
## Build STAR index
## -----------------------------------------------------------------------------
rule star_index:
    input:
        genome = "reference/GRCh38.primary_assembly.genome.fa",
        gtf = "reference/gencode.v41.annotation.protein_coding.expanded.gtf"
    output: 
        idx = "reference/GRCh38.primary_assembly_gencode.v41.annotation.protein_coding.expanded.starindex/SA"
    params:
        idxdir = lambda wildcards, output: os.path.dirname(output["idx"])
    threads: 
        16
    envmodules:
        f"{STARmod}"
    shell:
        '''
        STAR --runMode genomeGenerate --runThreadN {threads} --genomeDir {params.idxdir}  \
        --genomeFastaFiles {input.genome} --sjdbGTFfile {input.gtf} --sjdbOverhang 100
        '''

## -----------------------------------------------------------------------------
## Align with STAR and quantify with Salmon
## -----------------------------------------------------------------------------
rule star_align:
    input:
        idx = "reference/GRCh38.primary_assembly_gencode.v41.annotation.protein_coding.expanded.starindex/SA",
        r1 = "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        r2 = "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz"
    output:
        bam = "{simtype}/2_quants/star/sample{i}/Aligned.toTranscriptome.out.bam"
    params:
        idxdir = lambda wildcards, input: os.path.dirname(input["idx"]),
        outprefix = lambda wildcards, output: os.path.dirname(output["bam"]) + "/"
    threads: 
        16
    envmodules:
        f"{STARmod}"
    shell:
        '''
        STAR --runMode alignReads --runThreadN {threads} --genomeDir {params.idxdir} \
		--readFilesCommand zcat --readFilesIn {input.r1} {input.r2} \
		--outFileNamePrefix {params.outprefix} --outSAMtype BAM SortedByCoordinate \
        --quantMode TranscriptomeSAM
        '''

rule run_salmon_on_alignments:
    input:
        txome = "reference/gencode.v41.annotation.protein_coding.expanded.transcripts.fa",
        bam = "{simtype}/2_quants/star/sample{i}/Aligned.toTranscriptome.out.bam"
    output:
        quant = "{simtype}/2_quants/salmon_star/sample{i}/quant.sf"
    params:
        outdir = lambda wildcards, output: os.path.dirname(output["quant"])
    threads:
        16
    envmodules:
        f"{Salmonmod}"
    shell:
        '''
        salmon quant -t {input.txome} --libType A -a {input.bam} \
        --threads {threads} --output {params.outdir} --dumpEq --numGibbsSamples 30
        '''

## -----------------------------------------------------------------------------
## Build kallisto index
## -----------------------------------------------------------------------------
rule build_kallisto_index:
    input:
        txome = "reference/gencode.v41.annotation.protein_coding.expanded.transcripts.fa",
    output:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded.k31.kidx"
    threads:
        16
    envmodules:
        f"{kallistomod}"
    shell:
        '''
        kallisto index -i {output.idx} -k 31 {input.txome}
		'''

## -----------------------------------------------------------------------------
## Quantify with kallisto
## -----------------------------------------------------------------------------
rule run_kallisto_abundance:
    input:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded.k31.kidx",
        r1 = "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        r2 = "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz"
    output:
        quant = "{simtype}/2_quants/kallisto_quant/sample{i}/abundance.tsv"
    params:
        outdir = lambda wildcards, output: os.path.dirname(output["quant"])
    threads:
        16
    envmodules:
        f"{kallistomod}"
    shell:
        '''
        kallisto quant -i {input.idx} -o {params.outdir} --bias --threads {threads} \
        {input.r1} {input.r2}
        '''

rule run_kallisto_ec:
    input:
        idx = "reference/gencode.v41.annotation.protein_coding.expanded.k31.kidx",
        r1 = "{simtype}/1_reads/reads/sample{i}/sample{i}_1.fq.gz",
        r2 = "{simtype}/1_reads/reads/sample{i}/sample{i}_2.fq.gz",
        quant = "{simtype}/2_quants/kallisto_quant/sample{i}/abundance.tsv"
    output:
        pseudo = "{simtype}/2_quants/kallisto_pseudo/sample{i}/pseudoalignments.tsv",
        ec = "{simtype}/2_quants/kallisto_pseudo/sample{i}/pseudoalignments.ec"
    params:
        outdir = lambda wildcards, output: os.path.dirname(output["pseudo"])
    threads:
        16
    envmodules:
        f"{kallistomod}"
    shell:
        '''
        kallisto pseudo -i {input.idx} -o {params.outdir} --threads {threads} \
        {input.r1} {input.r2}
        '''